// Java 1.8.0_281, Gradle 6.5
plugins {
    id "net.minecraftforge.gradle.forge" version "FG_2.1-SNAPSHOT"
    id "org.spongepowered.mixin" version "d5f9873d60"
    id "java"
}

version = "1.2.8-BETA4"
group = "mrfast.sbf"
archivesBaseName = "Skyblock-Features"

compileJava.options.encoding = 'UTF-8'

sourceSets {
    main {
        output.resourcesDir = java.outputDir
    }
}

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false
    clientRunArgs += '--mixin mixins.skyblockfeatures.json --tweakClass org.spongepowered.asm.launch.MixinTweaker --tweakClass gg.essential.loader.stage0.EssentialSetupTweaker'
}

repositories {
    mavenCentral()
    jcenter();
    maven { url "https://repo.spongepowered.org/repository/maven-public/" }
    maven { url "https://jitpack.io/" }
    maven { url "https://repo.nea.moe/releases"}
    maven { url "https://repo.essential.gg/repository/maven-public" }
}


configurations {
    packageLib
    implementation.extendsFrom(packageLib)
}


dependencies {
    packageLib "gg.essential:loader-launchwrapper:1.1.1"
    implementation "gg.essential:essential-1.8.9-forge:12132+g6e2bf4dc5"

    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.10'

    implementation "org.spongepowered:mixin:0.7.11-SNAPSHOT"
    implementation 'org.jetbrains:annotations:24.0.0'
    implementation("moe.nea:libautoupdate:1.2.0")

    implementation("org.spongepowered:mixin:0.7.11-SNAPSHOT")
    annotationProcessor("org.spongepowered:mixin:0.7.11-SNAPSHOT")
}

mixin {
    add sourceSets.main, "mixins.skyblockfeatures.refmap.json"
}

def tweakClass = "org.spongepowered.asm.launch.MixinTweaker"

task setDevEnv {
    doLast {
        if (project.hasProperty('devEnv')) {
            tweakClass = project.devEnv.toBoolean()
                    ? "gg.essential.loader.stage0.EssentialSetupTweaker"
                    : "org.spongepowered.asm.launch.MixinTweaker"
        }
    }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest.attributes(
            "ForceLoadAsMod": true,
            "MixinConfigs": "mixins.skyblockfeatures.json",
            "FMLAT": "mrfast_at.cfg",
            "ModSide": "CLIENT",
            "TweakOrder": "0"
    )

    // Dynamically set the tweak class based on the condition
    doLast {
        manifest.attributes(
                "TweakClass": tweakClass
        )
    }

    from {
        configurations.packageLib.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
jar.dependsOn setDevEnv


processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

println 'Attempting to compile SBF at \u001B[33m'+(new Date())+"\u001B[0m"

tasks.register('finalize') {
    doLast {
        println 'Mod Compiled at \u001B[33m' + (new Date()) + "\u001B[0m"
    }
}
build.finalizedBy(finalize)